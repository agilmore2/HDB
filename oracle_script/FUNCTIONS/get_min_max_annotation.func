CREATE OR REPLACE FUNCTION GET_MIN_MAX_ANNOTATION 
  (sdi IN integer,
   year IN varchar2,
   value_in IN float)
  RETURN varchar2
IS
  date_string VARCHAR2(50);
  min_date DATE;
  max_date DATE;
  value_count INTEGER;
BEGIN

  SELECT count(value), min(start_date_time), max(start_date_time)
  INTO value_count, min_date, max_date
  FROM r_day
  WHERE to_char (start_date_time,'yyyy') = year
    AND site_datatype_id = sdi
    AND value = value_in;

  IF value_count = 0 THEN
    date_string := 'ERROR';
  ELSIF value_count = 1 THEN
    date_string := to_char(min_date,'FMMonth DD');
  ELSIF value_count = 2 THEN
    IF (to_char(min_date,'mm') = to_char(max_date,'mm')) THEN
      date_string := to_char(min_date,'FMMonth DD')||', '|| to_char(max_date,'FMDD');
    ELSE
      date_string := to_char(min_date,'FMMonth DD')||', '|| to_char(max_date,'FMMonth DD');
    END IF;
  ELSIF (max_date - min_date + 1 = value_count) THEN
    IF (to_char(min_date,'mm') = to_char(max_date,'mm')) THEN
      date_string := to_char(min_date,'FMMonth DD')||' - '|| to_char(max_date,'FMDD');
    ELSE
      date_string := to_char(min_date,'FMMonth DD')||' - '|| to_char(max_date,'FMMonth DD');
    END IF;
  ELSE
    date_string := 'numerous occasions';
  END IF;

  return (date_string);
END;
/

show errors
GRANT EXECUTE ON GET_MIN_MAX_ANNOTATION TO PUBLIC;
CREATE OR REPLACE PUBLIC SYNONYM GET_MIN_MAX_ANNOTATION FOR GET_MIN_MAX_ANNOTATION;
